buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

allprojects {
    group = 'net.benrowland.stravaheatmap'
    version = '1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'idea'
}

subprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    // Issue #9 - we have to copy all Gradle JVM args to the forked test process so we can set the
    // datasource properties when running on Travis CI.
    tasks.withType(Test) {
        systemProperties = System.properties

        // Jacoco requires this after overriding systemProperties above
        systemProperties['user.dir'] = workingDir
    }

    apply plugin: "jacoco"
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("mysql:mysql-connector-java")
    compile("org.hibernate:hibernate-java8")
    compile("org.mockito:mockito-all:1.10.19")

    testCompile("org.springframework.boot:spring-boot-starter-test")
}
